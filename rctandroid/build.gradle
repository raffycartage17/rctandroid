plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.racartech.library.rctandroid'
    compileSdkVersion 36
    group 'com.racartech.library'
    version '1.4.0'

    publishing {
        singleVariant('release') {
            //withSourcesJar() // Automatically adds sources JAR
            withJavadocJar()
        }
    }

    defaultConfig {
        minSdk 30
        targetSdk 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }


    sourceSets {
        main {
            res.srcDirs += 'src/main/res'
            assets.srcDirs += 'src/main/assets'
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {

    implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-android'

    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.9.2")

    implementation platform('com.google.firebase:firebase-bom:34.0.0') //33.11.0 causes NullPointerException, Upgrade gradle first
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-functions'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-inappmessaging'
    implementation 'com.google.firebase:firebase-inappmessaging-display'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-config'

    implementation 'com.github.instacart:truetime-android:3.5'


    implementation 'com.google.zxing:core:3.5.3' //3.3.3
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'

    implementation 'commons-io:commons-io:2.20.0' //2.16.1

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'androidx.core:core-ktx:1.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    implementation 'com.google.code.gson:gson:2.13.1' //2.11.0
    implementation 'com.google.android.gms:play-services-location:21.3.0' //22.2.0
    implementation 'com.google.maps:google-maps-services:2.2.0'
    implementation 'com.google.maps.android:android-maps-utils:3.14.0'

    implementation 'com.github.crazzyghost:alphavantage-java:1.7.0'

    implementation 'com.github.adaptech-cz.Tesseract4Android:tesseract4android:4.4.0'

    implementation 'androidx.appcompat:appcompat:1.7.1' //1.6.1
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0' //1.1.5
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0' //3.5.1
}



tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId = "com.racartech.library"
                artifactId = "rctandroid"
                version = "1.4.0"
                artifact tasks.named('androidSourcesJar').get()
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir("maven-repo")
            }
        }
    }
}


